# Utility function (copied from LLVM).
function (add_sphinx_target builder)
	set(SPHINX_BUILD_DIR "${CMAKE_CURRENT_BINARY_DIR}/${builder}")
	set(SPHINX_DOC_TREE_DIR "${CMAKE_CURRENT_BINARY_DIR}/_doctrees")
	set(SPHINX_TARGET_NAME docs-${builder})
	
	set(SPHINX_BUILDER_NAME "${builder}")
	if ("${builder}" STREQUAL "pdf")
		# PDF documentation is generated by producing LaTeX.
		set(SPHINX_BUILDER_NAME "latex")
	endif()
	
	set(SPHINX_BUILDER_TARGET_NAME docs-${SPHINX_BUILDER_NAME})
	
	add_custom_target(${SPHINX_BUILDER_TARGET_NAME}
		COMMAND ${SPHINX_EXECUTABLE}
			-b ${SPHINX_BUILDER_NAME}
			-d "${SPHINX_DOC_TREE_DIR}"
			-q # Quiet: no output other than errors and warnings.
			-W # Warnings are errors.
			"${CMAKE_CURRENT_SOURCE_DIR}" # Source
			"${SPHINX_BUILD_DIR}" # Output
		COMMENT
			"Generating ${SPHINX_BUILDER_NAME} Sphinx documentation")
	
	if ("${builder}" STREQUAL "pdf")
		# Build LaTeX into PDF.
		add_custom_target(${SPHINX_TARGET_NAME}
			COMMAND make -C "${CMAKE_CURRENT_BINARY_DIR}/pdf" all-pdf
			COMMENT
				"Generating ${builder} Sphinx documentation")
		add_dependencies(${SPHINX_TARGET_NAME} ${SPHINX_BUILDER_TARGET_NAME})
	endif()
	
	# When "clean" target is run, remove the Sphinx build directory
	set_property(DIRECTORY APPEND PROPERTY
		ADDITIONAL_MAKE_CLEAN_FILES
		"${SPHINX_BUILD_DIR}")

	# We need to remove ${SPHINX_DOC_TREE_DIR} when make clean is run
	# but we should only add this path once
	get_property(_CURRENT_MAKE_CLEAN_FILES
		DIRECTORY PROPERTY ADDITIONAL_MAKE_CLEAN_FILES)
	list(FIND _CURRENT_MAKE_CLEAN_FILES "${SPHINX_DOC_TREE_DIR}" _INDEX)
	if (_INDEX EQUAL -1)
		set_property(DIRECTORY APPEND PROPERTY
			ADDITIONAL_MAKE_CLEAN_FILES
			"${SPHINX_DOC_TREE_DIR}")
	endif()

	add_dependencies(sphinx ${SPHINX_TARGET_NAME})
endfunction()

find_package(Sphinx REQUIRED)

add_custom_target(sphinx ALL)

if (${SPHINX_OUTPUT_HTML})
	add_sphinx_target(html)
endif()

if (${SPHINX_OUTPUT_MAN})
	add_sphinx_target(man)
endif()

if (${SPHINX_OUTPUT_PDF})
	add_sphinx_target(pdf)
endif()

